apply plugin: de.schauderhaft.ascii.reveal.AsciiReveal

asciiReveal {
    srcDir = "blah"
    srcFile = "index.ad"
    template = "schauderhaft"
}

apply plugin: 'java'


version = '1.0.0-SNAPSHOT'

ext {
    revealjsVersion = '3.4.1'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir, 'download')
    templateDir = new File(downloadDir, 'templates')
    revealjsDir = new File(downloadDir, 'reveal.js')
}

dependencies {
    gems 'rubygems:slim:3.0.6'
    gems 'rubygems:thread_safe:0.3.5'
    gems 'rubygems:asciidoctor-diagram:1.4.0'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {

            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir, recursive: true, overwrite: true
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                    revealjsDir, recursive: true, overwrite: true
        }
    }
}

download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
}

task themeResources(type: Copy) {
    dependsOn download
    from 'src/docs/sass' into 'build/download/reveal.js/css/theme'
}

tasks.withType(Copy) {
    eachFile { println it.file; println it.getPath() }
}

compileSass {
    dependsOn download, themeResources

    scssFile = 'src/docs/sass/schauderhaft.scss'
    cssFile = 'build/download/reveal.js/css/theme/schauderhaft.css'
}


asciidoctor {
    dependsOn jrubyPrepare, download, compileSass, asciireveal

    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir

    sources {
        include 'talk.ad'
    }
    resources {
        from(sourceDir) {
            include 'images/**'
        }
        from(downloadDir) {
            include 'reveal.js/**'
        }
    }
    backends 'revealjs'
    attributes    \
           'build-gradle': file('build.gradle'),
            'sourcedir': project.sourceSets.main.java.srcDirs[0],
            'endpoint-url': 'http://example.org',
            'source-highlighter': 'highlightjs',
            'highlightjs-theme': 'reveal.js/lib/css/zenburn.css',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': 'slide-',
            'idseparator': '-',
            'docinfo1': '',
            'revealjs_theme': 'schauderhaft',
            'revealjs_transition': 'linear',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'false',
            'revealjs_controls' : 'false',
            'conference': 'Javaland 2017'
    options template_dirs: [new File(templateDir, 'templates/slim').absolutePath]
}